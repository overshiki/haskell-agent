-- | Static DeepSeek API-key credentials.
module Agent.DeepSeek.Credential
    ( staticApiKeyProvider
    , credentialFromApiKey
    , credentialFromEnv
    ) where

import Agent.Error (ApiError(..), ErrorType(..))
import Agent.Provider
import Data.Maybe (fromMaybe)
import Data.Text (Text)
import qualified Data.Text as Text
import Data.Time.Clock (addUTCTime, getCurrentTime)
import System.Environment (lookupEnv)

-- | A single DeepSeek API key with no OAuth refresh or account failover.
staticApiKeyProvider :: Text -> TokenProvider
staticApiKeyProvider apiKey = tokenProvider ApiBilled \failed -> case failed of
        Nothing ->
            pure $ Right (credentialFromApiKey apiKey)
        Just FailedCredential
            { failure = AccountRateLimited { retryAfterSeconds }
            , failureReason
            } -> do
            now <- getCurrentTime
            let seconds = max 1
                    (fromMaybe staticApiKeyRateLimitCooldownSeconds retryAfterSeconds)
            pure $ Left $ CredentialsExhausted
                { retryAt = addUTCTime (fromIntegral seconds) now
                , exhaustionReasons = [failureReason]
                }
        Just FailedCredential { failure = AccountAuthenticationRejected } ->
            pure $ Left $ ProviderError AuthenticationError
                "static DeepSeek API key was rejected"
                Nothing

staticApiKeyRateLimitCooldownSeconds :: Int
staticApiKeyRateLimitCooldownSeconds = 60

credentialFromApiKey :: Text -> Credential
credentialFromApiKey apiKey = Credential
    { accessToken = apiKey
    , accountId = ""
    , leaseId = Nothing
    , provider = DeepSeekProvider
    }

-- | Read @DEEPSEEK_API_KEY@. Empty or unset yields 'Nothing'.
credentialFromEnv :: IO (Maybe Credential)
credentialFromEnv = do
    key <- lookupEnv "DEEPSEEK_API_KEY"
    pure $ case key of
        Just value | not (null value) -> Just (credentialFromApiKey (Text.pack value))
        _ -> Nothing
