module Agent.CLI.ProviderTransitionSpec (spec) where

import Agent.CLI.Models (ModelTarget(..))
import Agent.CLI.Options (CliOptions(..), defaultCliOptions, isOneShot)
import Agent.CLI.ProviderTransition
import Agent.Dialect (DialectId(..))
import Agent.Provider (BillingMode(..), Provider(..))
import Agent.Tools.PlanMode (PlanModeState(..))
import Data.IORef (newIORef, readIORef)
import Data.Maybe (isNothing)
import qualified Data.Set as Set
import Data.Text (Text)
import Test.Hspec

spec :: Spec
spec = do
    describe "applyProviderTransition" do
        it "preserves one-shot invocation mode without requiring persistence" do
            let options = defaultCliOptions
                    { optPrompt = Just "hello"
                    , optProvider = Just XAIProvider
                    }
                transitioned = applyProviderTransition options
                    (transition Nothing Nothing)
            isOneShot transitioned `shouldBe` True
            transitioned.optPrompt `shouldBe` Just "hello"
            transitioned.optResume `shouldBe` Nothing
            transitioned.optProvider `shouldBe` Just OpenAIProvider
            transitioned.optModel `shouldBe` Just "gpt-5.6-sol"

        it "uses a persisted session when one exists" do
            let transitioned = applyProviderTransition defaultCliOptions
                    (transition (Just "session-1") Nothing)
            transitioned.optResume `shouldBe` Just (Just "session-1")

    describe "setPendingExitAfter" do
        it "preserves the plan state while changing exit behavior" do
            let pending = PendingTurn
                    { pendingPromptText = "make a plan"
                    , pendingInputs = []
                    , pendingCheckpointed = False
                    , pendingExitAfter = False
                    , pendingPlanState = PlanActive
                    }
                updated = setPendingExitAfter True pending
            updated.pendingExitAfter `shouldBe` True
            updated.pendingPlanState `shouldBe` PlanActive

    describe "resumePendingTurnIfPresent" do
        it "atomically claims and resumes the failed turn once" do
            failedTurnRef <- newIORef (Just pendingTurn)

            result <- resumePendingTurnIfPresent
                failedTurnRef
                (pure . (.pendingPromptText))
                (pure "no failed turn")

            result `shouldBe` "continue the conversation"
            fmap isNothing (readIORef failedTurnRef) `shouldReturn` True
            resumePendingTurnIfPresent
                failedTurnRef
                (pure . (.pendingPromptText))
                (pure "already claimed")
                `shouldReturn` "already claimed"

        it "continues normally when no failed turn is stored" do
            failedTurnRef <- newIORef Nothing

            result <- resumePendingTurnIfPresent
                failedTurnRef
                (\_ -> pure ("unexpected retry" :: Text))
                (pure ("no failed turn" :: Text))

            result `shouldBe` "no failed turn"

    describe "transitionCommitsImmediately" do
        it "keeps a startup automatic fallback provisional" do
            transitionCommitsImmediately (transition Nothing Nothing)
                `shouldBe` False

        it "commits a manual selection immediately" do
            let manual =
                    (transition Nothing Nothing)
                        { transitionCause = ManualTransition }
            transitionCommitsImmediately manual `shouldBe` True

transition
    :: Maybe Text
    -> Maybe PendingTurn
    -> ProviderTransition
transition sessionId pending = ProviderTransition
    { transitionTarget = ModelTarget
        { targetProvider = OpenAIProvider
        , targetConnectionId = "openai"
        , targetModelId = "gpt-5.6-sol"
        , targetWireModelId = "gpt-5.6-sol"
        , targetDialect = CodexDialect
        }
    , transitionAccountSelectionId = Nothing
    , transitionAccountId = Nothing
    , transitionSessionId = sessionId
    , transitionPendingTurn = pending
    , transitionUnavailableProviders = Set.singleton XAIProvider
    , transitionCause = AutomaticFallback
    , transitionAutomaticBilling = Just SubscriptionBilled
    }

pendingTurn :: PendingTurn
pendingTurn = PendingTurn
    { pendingPromptText = "continue the conversation"
    , pendingInputs = []
    , pendingCheckpointed = False
    , pendingExitAfter = False
    , pendingPlanState = PlanInactive
    }
