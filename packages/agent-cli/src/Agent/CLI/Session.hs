-- | Persist REPL conversations under @~/.haskell-agent/sessions@.
module Agent.CLI.Session
    ( SessionHandle(..)
    , SessionMeta(..)
    , SessionPromptSnapshot(..)
    , sessionMetaDecoder
    , LegacySubagentTarget(..)
    , TranscriptEffect(..)
    , SessionTurn(..)
    , sessionTurnDecoder
    , SessionTurnPage(..)
    , SessionResumeStats(..)
    , SessionActivity(..)
    , sessionActivityDecoder
    , SessionTransfer(..)
    , sessionTransferDecoder
    , SessionCreate(..)
    , Persistence(..)
    , PersistenceState(..)
    , newPendingPersistence
    , newPendingPersistenceReserved
    , newActivePersistence
    , persistenceTempDir
    , setPersistenceActivity
    , clearPersistenceActivity
    , loadSessionActivity
    , cleanupPendingPersistence
    , createSession
    , forkSession
    , forkSessionAt
    , appendTurn
    , appendTurnIndexed
    , appendTurnWithMetaUpdate
    , appendTurnWithMetaUpdateIndexed
    , appendTurnWithPromptResetIndexed
    , appendTurnKeepTitle
    , appendTurnKeepTitleIndexed
    , sessionRewindChoices
    , rewindSession
    , addSessionUsage
    , deleteSession
    , loadSession
    , loadSessions
    , loadActiveSession
    , loadSessionMeta
    , loadRecentSessionTurns
    , loadSessionTurnsBefore
    , loadSessionTurnsAfter
    , loadSessionResumeStats
    , importSessionTransfer
    , loadSessionHandle
    , isValidSessionId
    , listSessions
    , sessionDirForId
    , sessionTempDirForId
    , sessionTempsRoot
    , allocateSessionTemp
    , ensureSessionTemp
    , removeSessionTemp
    , cleanupStaleSessionTemps
    , defaultSessionTempKeepCount
    , SessionTempCleanupReport(..)
    , SessionTempLease
    , acquireSessionTempLease
    , releaseSessionTempLease
    , sessionsRoot
    , sessionTitleFromPrompt
    , setGeneratedSessionTitle
    , setManualSessionTitle
    , resetSessionTitleToAuto
    , setSessionRecap
    , setSessionTurnSummary
    , sessionConversationText
    , sessionLegacySubagentTarget
    , sessionTitleTurnCountFromSlot
    , writeSessionMeta
    , compatibleSessionPromptSnapshot
    , ensureSession
    , ensureSessionWithPromptSnapshot
    , resumeHint
    , sessionUsageFromTurns
    ) where

import Agent.FileRetry (retryOnFileBusy, writeLazyFileAtomically)
import Agent.CLI.SessionLock
    ( acquireSessionLock
    , releaseSessionLock
    )
import Agent.CLI.Json (decodeLazy)
import Agent.CLI.Request
    ( requestPromptParts
    , requestToolIdentities
    )
import Agent.CLI.Session.Types
    ( SessionHandle(..)
    , SessionMeta(..)
    , SessionPromptSnapshot(..)
    , sessionMetaDecoder
    , LegacySubagentTarget(..)
    , TranscriptEffect(..)
    , SessionTurn(..)
    , sessionTurnDecoder
    , SessionTurnPage(..)
    , SessionResumeStats(..)
    , SessionActivity(..)
    , sessionActivityDecoder
    , SessionTransfer(..)
    , sessionTransferDecoder
    , SessionCreate(..)
    , Persistence(..)
    , PersistenceState(..)
    )
import Agent.CLI.Session.Codec
    ( contentFingerprint
    , decodeStoredSession
    , fromStoredMetadata
    , fromStoredTurn
    , importLegacySession
    , toStoredMetadata
    , toStoredPromptSnapshot
    , toStoredTurn
    , validateSessionMeta
    )
import Agent.CLI.Models (ModelTarget(..))
import Agent.CLI.SessionTitle (titleRefreshIndex)
import Agent.Dialect (DialectId)
import Agent.Loop (TokenUsage(..))
import Agent.OpenAI.Compaction (rewindSessionUserText)
import Agent.OsPath (toText, unsafeToFilePath)
import Agent.Provider (Provider)
import Agent.Responses.Types (ResponseCreateParams(model))
import Agent.Store.Postgres (normalizePostgresTimestamp)
import Agent.Store.Postgres.Connection (StorePool)
import qualified Agent.Store.Postgres.Session as Store
import Agent.Store.Types (StoreError, renderStoreError)
import Control.Applicative ((<|>))
import Control.Exception.Safe
    ( SomeException
    , displayException
    , finally
    , mask
    , onException
    , tryAny
    , tryIO
    )
import Control.Monad (foldM, guard, unless, when)
import Control.Monad.Trans.Class (lift)
import Control.Monad.Trans.Except
    ( ExceptT(..)
    , except
    , runExceptT
    , throwE
    )
import qualified Data.Aeson as Aeson
import qualified Data.ByteString.Lazy as LBS
import Data.Char (isHexDigit)
import Data.Int (Int64)
import Data.IORef
import Data.Foldable (foldl')
import Data.Functor ((<&>))
import Data.List (sortOn)
import Data.Maybe (fromMaybe)
import Data.Ord (Down(..))
import Data.Text (Text)
import qualified Data.Text as Text
import Data.Time.Calendar (Day)
import Data.Time.Clock
    ( UTCTime
    , getCurrentTime
    , nominalDiffTimeToSeconds
    , utctDay
    )
import Data.Time.Clock.POSIX (utcTimeToPOSIXSeconds)
import Data.Time.Format (defaultTimeLocale, formatTime, parseTimeM)
import qualified Data.Vector as Vector
import Numeric (showHex)
import System.Directory.OsPath
    ( copyFile
    , createDirectory
    , createDirectoryIfMissing
    , doesDirectoryExist
    , doesFileExist
    , listDirectory
    , removePathForcibly
    , removeFile
    )
import qualified System.FileLock as FileLock
import Agent.OsPath (unsafeEncodeUtf)
import System.OsPath
    ( OsPath
    , equalFilePath
    , normalise
    , takeDirectory
    , takeFileName
    , (</>)
    )
import System.IO.Error (isDoesNotExistError)
import System.Posix.Files
    ( FileStatus
    , getSymbolicLinkStatus
    , isDirectory
    , isRegularFile
    , isSymbolicLink
    , setFileMode
    )

sessionSchemaVersion :: Int
sessionSchemaVersion = 1

-- | Keep a small recent cache for session artifacts while bounding abandoned
-- shell environments, tool outputs, and other scratch data.
defaultSessionTempKeepCount :: Int
defaultSessionTempKeepCount = 15

data SessionTempCleanupReport = SessionTempCleanupReport
    { tempCleanupRemoved :: ![OsPath]
    , tempCleanupFailures :: ![(OsPath, Text)]
    }
    deriving (Eq, Show)

instance Semigroup SessionTempCleanupReport where
    left <> right = SessionTempCleanupReport
        { tempCleanupRemoved =
            left.tempCleanupRemoved <> right.tempCleanupRemoved
        , tempCleanupFailures =
            left.tempCleanupFailures <> right.tempCleanupFailures
        }

instance Monoid SessionTempCleanupReport where
    mempty = SessionTempCleanupReport [] []

newtype SessionTempLease = SessionTempLease FileLock.FileLock

-- | Reuse an immutable provider prefix only when the runtime target and the
-- ordered provider-visible tool identities still describe the same session.
-- Tool documentation/schema bytes may evolve between binaries; the persisted
-- versions remain authoritative until a tool is added, removed, reordered, or
-- renamed.
compatibleSessionPromptSnapshot
    :: Provider
    -> Text
    -> DialectId
    -> OsPath
    -> Maybe Text
    -> ResponseCreateParams
    -> Maybe SessionPromptSnapshot
    -> Maybe SessionPromptSnapshot
compatibleSessionPromptSnapshot
    provider connection dialect cwd sessionId params maybeSnapshot = do
        cacheKey <- sessionId
        snapshot <- maybeSnapshot
        let currentTools = snd (requestPromptParts params)
        guard (snapshot.promptSnapshotVersion == 1)
        guard (snapshot.promptSnapshotProvider == provider)
        guard (snapshot.promptSnapshotConnection == connection)
        guard (Just snapshot.promptSnapshotModel == params.model)
        guard (snapshot.promptSnapshotDialect == dialect)
        guard (snapshot.promptSnapshotCwd == cwd)
        guard (snapshot.promptSnapshotCacheKey == cacheKey)
        guard
            ( requestToolIdentities snapshot.promptSnapshotTools
                == requestToolIdentities currentTools
            )
        pure snapshot

-- | @~/.haskell-agent/sessions@ given the user's home directory.
sessionsRoot :: OsPath -> OsPath
sessionsRoot home =
    home </> unsafeEncodeUtf ".haskell-agent" </> unsafeEncodeUtf "sessions"

-- | @~/.haskell-agent/tmp/sessions@ for a corresponding sessions root.
sessionTempsRoot :: OsPath -> OsPath
sessionTempsRoot root =
    takeDirectory root
        </> unsafeEncodeUtf "tmp"
        </> unsafeEncodeUtf "sessions"

newPendingPersistence :: SessionCreate -> IO Persistence
newPendingPersistence spec = do
    (sessionId, tempDir) <- allocateSessionTemp spec.createRoot
    newPendingPersistenceReserved spec sessionId tempDir

newPendingPersistenceReserved
    :: SessionCreate
    -> Text
    -> OsPath
    -> IO Persistence
newPendingPersistenceReserved spec sessionId tempDir = do
    expected <- either (fail . Text.unpack) pure
        (sessionTempDirForId spec.createRoot sessionId)
    unless (expected == tempDir) $
        fail "reserved session temp directory does not match session id"
    ensurePrivateDir tempDir
    PersistenceEnabled
        <$> newIORef (PersistencePending spec sessionId tempDir)

newActivePersistence :: SessionHandle -> IO Persistence
newActivePersistence handle = do
    ensurePrivateDir handle.sessionTempDir
    persistence <-
        PersistenceEnabled <$> newIORef (PersistenceActive handle)
    -- A prior process may have died while a cooldown/retry marker was active.
    -- Never attribute that stale activity to a newly resumed turn.
    clearPersistenceActivity persistence
    pure persistence

persistenceTempDir :: Persistence -> IO (Maybe OsPath)
persistenceTempDir = \case
    PersistenceDisabled -> pure Nothing
    PersistenceEnabled slotRef ->
        readIORef slotRef <&> \case
            PersistencePending _ _ tempDir -> Just tempDir
            PersistenceActive handle -> Just handle.sessionTempDir

setPersistenceActivity
    :: Persistence
    -> Text
    -> Text
    -> Maybe UTCTime
    -> IO ()
setPersistenceActivity persistence kind message retryAt =
    persistenceTempDir persistence >>= \case
        Nothing -> pure ()
        Just tempDir -> do
            _ <- tryIO do
                ensurePrivateDir tempDir
                now <- getCurrentTime
                writeLazyFileAtomically
                    (sessionActivityPath tempDir)
                    0o600
                    (Aeson.encode SessionActivity
                        { activityKind = kind
                        , activityMessage = message
                        , activityRetryAt = retryAt
                        , activityUpdatedAt = now
                        })
            pure ()

clearPersistenceActivity :: Persistence -> IO ()
clearPersistenceActivity persistence =
    persistenceTempDir persistence >>= mapM_ \tempDir -> do
        _ <- tryIO (removeFile (sessionActivityPath tempDir))
        pure ()

loadSessionActivity
    :: OsPath
    -> Text
    -> IO (Maybe SessionActivity)
loadSessionActivity root sessionId =
    case sessionTempDirForId root sessionId of
        Left _ -> pure Nothing
        Right tempDir -> do
            let path = sessionActivityPath tempDir
            exists <- doesFileExist path
            if not exists
                then pure Nothing
                else
                    tryIO (retryOnFileBusy (LBS.readFile (unsafeToFilePath path)))
                        <&> \case
                            Left _ -> Nothing
                            Right bytes ->
                                either (const Nothing) Just
                                    (decodeLazy sessionActivityDecoder bytes)

sessionActivityPath :: OsPath -> OsPath
sessionActivityPath tempDir =
    tempDir </> unsafeEncodeUtf "activity.json"

-- | Remove scratch space only when a reserved session never became durable.
cleanupPendingPersistence :: Persistence -> IO ()
cleanupPendingPersistence = \case
    PersistenceDisabled -> pure ()
    PersistenceEnabled slotRef ->
        readIORef slotRef >>= \case
            PersistencePending spec sessionId _ -> do
                _ <- removeSessionTemp spec.createRoot sessionId
                pure ()
            PersistenceActive _ -> pure ()

-- | Create durable session state using a store-owned pool.
--
-- This function does not acquire or own a database pool. Each database
-- operation uses the pool's bracketed 'Pool.use' path, while the enclosing
-- 'Store' owns and releases the pool.
createSession :: SessionCreate -> IO SessionHandle
createSession spec = do
    (sessionId, tempDir) <- allocateSessionTemp spec.createRoot
    createReservedSession spec sessionId tempDir Nothing
        `onException` removeReservedTemp spec.createRoot sessionId

-- | Clone a persisted session and its durable branch artifacts under a new
-- id. The returned handle is not locked or installed as the active session;
-- callers should use the same active-session handoff used by @/new@.
forkSession
    :: OsPath
    -> SessionHandle
    -> [SessionTurn]
    -> Maybe Text
    -> IO (Either Text SessionHandle)
forkSession root source turns requestedTitle =
    forkSessionAt
        root
        source
        turns
        requestedTitle
        source.sessionMeta.metaCwd
        source.sessionMeta.metaGitBranch

-- | Clone a persisted session while selecting the fork's working directory
-- and git branch link. This is used by @/fork --worktree@; ordinary callers
-- retain the source cwd and branch through 'forkSession'.
forkSessionAt
    :: OsPath
    -> SessionHandle
    -> [SessionTurn]
    -> Maybe Text
    -> OsPath
    -> Maybe Text
    -> IO (Either Text SessionHandle)
forkSessionAt root source turns requestedTitle targetCwd gitBranch
    | not (any substantiveTurn (activeTranscriptTurns turns)) =
        pure (Left "a session must contain at least one turn before it can be forked")
    | otherwise = mask \restore -> do
        allocated <- tryIO (restore (allocateSessionTemp root))
        case allocated of
            Left err ->
                pure (Left ("could not reserve fork session: " <> exceptionText err))
            Right (sessionId, tempDir) ->
                case sessionDirForId root sessionId of
                    Left err -> do
                        cleanupForkFiles root sessionId Nothing
                        pure (Left err)
                    Right dir -> do
                        now <- normalizePostgresTimestamp <$> getCurrentTime
                        let meta =
                                forkedMetadata
                                    now
                                    sessionId
                                    requestedTitle
                                    targetCwd
                                    gitBranch
                                    source.sessionMeta
                            storedMeta = toStoredMetadata meta
                            handle = SessionHandle
                                { sessionPool = source.sessionPool
                                , sessionDir = dir
                                , sessionTempDir = tempDir
                                , sessionMetaPath =
                                    dir </> unsafeEncodeUtf "meta.json"
                                , sessionTranscriptPath =
                                    dir </> unsafeEncodeUtf "transcript.jsonl"
                                , sessionMeta = meta
                                }
                            cleanupFiles =
                                cleanupForkFiles root sessionId (Just dir)
                            cleanupOwned = do
                                cleanupForkDatabaseIfOwned
                                    source.sessionPool
                                    storedMeta
                                cleanupFiles
                        prepared <- tryIO $
                            restore (prepareForkDirectory source.sessionDir dir)
                        case prepared of
                            Left err -> do
                                cleanupForkFiles root sessionId Nothing
                                pure
                                    (Left
                                        ("could not copy fork session artifacts: "
                                            <> exceptionText err))
                            Right () -> do
                                stored <-
                                    restore
                                        (Store.createSessionFromSnapshot
                                            source.sessionPool
                                            storedMeta
                                            (map toStoredTurn turns))
                                        `onException` cleanupOwned
                                case stored of
                                    Left err -> do
                                        cleanupOwned
                                        pure (Left (renderStoreError err))
                                    Right False -> do
                                        cleanupFiles
                                        pure
                                            (Left
                                                "could not allocate a unique PostgreSQL session id")
                                    Right True -> pure (Right handle)
  where
    exceptionText = Text.pack . displayException

substantiveTurn :: SessionTurn -> Bool
substantiveTurn turn =
    turn.turnEffect /= TranscriptReset
        && ( not (Text.null (Text.strip turn.turnUserText))
            || maybe False (not . Text.null . Text.strip)
                turn.turnAssistantText
            || maybe False (not . Text.null . Text.strip) turn.turnError
            || not (null turn.turnItems)
           )

activeTranscriptTurns :: [SessionTurn] -> [SessionTurn]
activeTranscriptTurns =
    reverse
        . takeWhile ((/= TranscriptReset) . (.turnEffect))
        . reverse

forkedMetadata
    :: UTCTime
    -> Text
    -> Maybe Text
    -> OsPath
    -> Maybe Text
    -> SessionMeta
    -> SessionMeta
forkedMetadata now sessionId requestedTitle targetCwd gitBranch source =
    source
        { metaId = sessionId
        , metaCreatedAt = now
        , metaUpdatedAt = now
        , metaCwd = targetCwd
        , metaGitBranch = gitBranch
        , metaTitle = title
        , metaTitleIsManual =
            maybe source.metaTitleIsManual (const True) normalizedTitle
        , metaTitleRefreshIndex =
            maybe source.metaTitleRefreshIndex
                (const (max 2 source.metaTitleRefreshIndex))
                normalizedTitle
        , metaPromptSnapshot = Nothing
        }
  where
    normalizedTitle =
        requestedTitle
            >>= \raw ->
                let normalized = Text.unwords (Text.words raw)
                in if Text.null normalized then Nothing else Just normalized
    title = fromMaybe source.metaTitle normalizedTitle

prepareForkDirectory :: OsPath -> OsPath -> IO ()
prepareForkDirectory sourceDir destinationDir = do
    createDirectory destinationDir
    (do
        setFileMode (unsafeToFilePath destinationDir) 0o700
        copyOptionalArtifact
            ArtifactRegularFile
            (sourceDir </> unsafeEncodeUtf "plan.md")
            (destinationDir </> unsafeEncodeUtf "plan.md")
        copyOptionalArtifact
            ArtifactDirectory
            (sourceDir </> unsafeEncodeUtf "agents")
            (destinationDir </> unsafeEncodeUtf "agents"))
        `onException` do
            _ <- tryIO (removePathForcibly destinationDir)
            pure ()

data ArtifactKind
    = ArtifactRegularFile
    | ArtifactDirectory
    deriving (Eq)

copyOptionalArtifact :: ArtifactKind -> OsPath -> OsPath -> IO ()
copyOptionalArtifact expected source destination =
    symbolicLinkStatusMaybe source >>= \case
        Nothing -> pure ()
        Just status
            | isSymbolicLink status ->
                fail ("refusing to copy symbolic link: " <> Text.unpack (toText source))
            | expected == ArtifactRegularFile && isRegularFile status ->
                copyPrivateFile source destination
            | expected == ArtifactDirectory && isDirectory status ->
                copyPrivateDirectory source destination
            | otherwise ->
                fail ("unexpected fork artifact type: " <> Text.unpack (toText source))

copyPrivateDirectory :: OsPath -> OsPath -> IO ()
copyPrivateDirectory source destination = do
    createDirectory destination
    setFileMode (unsafeToFilePath destination) 0o700
    entries <- listDirectory source
    mapM_
        (\entry -> do
            let sourcePath = source </> entry
                destinationPath = destination </> entry
            status <- getSymbolicLinkStatus (unsafeToFilePath sourcePath)
            if isSymbolicLink status
                then
                    fail
                        ("refusing to copy symbolic link: "
                            <> Text.unpack (toText sourcePath))
                else if isDirectory status
                    then copyPrivateDirectory sourcePath destinationPath
                    else if isRegularFile status
                        then copyPrivateFile sourcePath destinationPath
                        else
                            fail
                                ("unexpected fork artifact type: "
                                    <> Text.unpack (toText sourcePath)))
        entries

copyPrivateFile :: OsPath -> OsPath -> IO ()
copyPrivateFile source destination = do
    copyFile source destination
    setFileMode (unsafeToFilePath destination) 0o600

symbolicLinkStatusMaybe :: OsPath -> IO (Maybe FileStatus)
symbolicLinkStatusMaybe path =
    tryIO (getSymbolicLinkStatus (unsafeToFilePath path)) >>= \case
        Left err
            | isDoesNotExistError err -> pure Nothing
            | otherwise -> ioError err
        Right status -> pure (Just status)

cleanupForkDatabaseIfOwned
    :: StorePool
    -> Store.SessionMetadata
    -> IO ()
cleanupForkDatabaseIfOwned pool expected = do
    loaded <- Store.loadSessionMetadata pool expected.sessionMetadataKey
    case loaded of
        Right (Just actual)
            | actual == expected -> do
                now <- getCurrentTime
                _ <- Store.deleteSession pool expected.sessionMetadataKey now
                pure ()
        _ -> pure ()

cleanupForkFiles :: OsPath -> Text -> Maybe OsPath -> IO ()
cleanupForkFiles root sessionId dir = do
    mapM_
        (\path -> do
            _ <- tryIO (removePathForcibly path)
            pure ())
        dir
    _ <- removeSessionTemp root sessionId
    pure ()

createReservedSession
    :: SessionCreate
    -> Text
    -> OsPath
    -> Maybe SessionPromptSnapshot
    -> IO SessionHandle
createReservedSession spec sessionId tempDir promptSnapshot = do
    let pool = spec.createPool
    ensurePrivateDir spec.createRoot
    dir <- either (fail . Text.unpack) pure
        (sessionDirForId spec.createRoot sessionId)
    createDirectory dir
    setFileMode (unsafeToFilePath dir) 0o700
    now <- normalizePostgresTimestamp <$> getCurrentTime
    let title = case spec.createTitleHint of
            Just hint | not (Text.null hint) -> hint
            _ -> "untitled"
        meta = SessionMeta
            { metaVersion = sessionSchemaVersion
            , metaId = sessionId
            , metaCreatedAt = now
            , metaUpdatedAt = now
            , metaProvider = spec.createTarget.targetProvider
            , metaConnection = spec.createTarget.targetConnectionId
            , metaModel = spec.createTarget.targetModelId
            , metaTransportModel = Just spec.createTarget.targetWireModelId
            , metaDialect = spec.createTarget.targetDialect
            , metaLegacySubagentTarget = Just LegacySubagentTarget
                { legacyTargetProvider = spec.createTarget.targetProvider
                , legacyTargetConnection = spec.createTarget.targetConnectionId
                , legacyTargetEffectiveModel =
                    spec.createTarget.targetWireModelId
                , legacyTargetDialect = spec.createTarget.targetDialect
                }
            , metaCwd = spec.createCwd
            , metaGitBranch = Nothing
            , metaEffort = spec.createEffort
            , metaTitle = title
            , metaTitleIsManual = spec.createTitleIsManual
            , metaTitleRefreshIndex = 0
            , metaTitleUserTurns = 0
            , metaLastResponseId = Nothing
            , metaInputTokens = 0
            , metaOutputTokens = 0
            , metaCachedTokens = 0
            , metaLastRecap = Nothing
            , metaLastTurnSummary = Nothing
            , metaLastRecapMainTurns = 0
            , metaPromptSnapshot = promptSnapshot
            }
        handle = SessionHandle
            { sessionPool = pool
            , sessionDir = dir
            , sessionTempDir = tempDir
            , sessionMetaPath = dir </> unsafeEncodeUtf "meta.json"
            , sessionTranscriptPath = dir </> unsafeEncodeUtf "transcript.jsonl"
            , sessionMeta = meta
            }
    let createStored = case promptSnapshot of
            Nothing -> Store.createSession pool (toStoredMetadata meta)
            Just snapshot ->
                Store.createSessionWithInitialPromptEpoch
                    pool
                    (toStoredMetadata meta)
                    (toStoredPromptSnapshot snapshot)
    createStored >>= \case
        Left err -> do
            _ <- tryIO (removePathForcibly dir)
            fail
                ("could not create PostgreSQL session: "
                    <> Text.unpack (renderStoreError err))
        Right False -> do
            _ <- tryIO (removePathForcibly dir)
            fail "could not allocate a unique PostgreSQL session id"
        Right True -> pure handle

-- | Create the session directory on first use when persistence is still pending.
ensureSession :: IORef PersistenceState -> IO SessionHandle
ensureSession slotRef = do
    slot <- readIORef slotRef
    case slot of
        PersistenceActive handle -> pure handle
        PersistencePending spec sessionId tempDir -> do
            handle <- createReservedSession spec sessionId tempDir Nothing
            writeIORef slotRef (PersistenceActive handle)
            pure handle

-- | Ensure the durable session exists and atomically persist the
-- provider-visible request prefix before it can be sent. Subsequent calls only
-- append an immutable epoch when the prefix or pending generated context
-- actually changes.
ensureSessionWithPromptSnapshot
    :: IORef PersistenceState
    -> SessionPromptSnapshot
    -> IO SessionHandle
ensureSessionWithPromptSnapshot slotRef candidate = do
    slot <- readIORef slotRef
    case slot of
        PersistencePending spec sessionId tempDir -> do
            handle <- createReservedSession
                spec
                sessionId
                tempDir
                (Just candidate)
            writeIORef slotRef (PersistenceActive handle)
            pure handle
        PersistenceActive handle -> do
            let snapshot =
                    maybe candidate
                        (`mergePromptSnapshotContext` candidate)
                        handle.sessionMeta.metaPromptSnapshot
            case handle.sessionMeta.metaPromptSnapshot of
                Just previous
                    | promptSnapshotsEquivalent previous snapshot ->
                        pure handle
                _ -> do
                    Store.appendSessionPromptEpoch
                        handle.sessionPool
                        handle.sessionMeta.metaId
                        (toStoredPromptSnapshot snapshot) >>= \case
                            Left err ->
                                fail
                                    ("could not persist PostgreSQL prompt epoch: "
                                        <> Text.unpack (renderStoreError err))
                            Right Nothing ->
                                fail
                                    ("session not found: "
                                        <> Text.unpack handle.sessionMeta.metaId)
                            Right (Just _) -> do
                                let next = handle
                                        { sessionMeta = handle.sessionMeta
                                            { metaPromptSnapshot = Just snapshot
                                            }
                                        }
                                writeIORef slotRef (PersistenceActive next)
                                pure next

mergePromptSnapshotContext
    :: SessionPromptSnapshot
    -> SessionPromptSnapshot
    -> SessionPromptSnapshot
mergePromptSnapshotContext previous candidate
    | promptSnapshotsSharePrefix previous candidate =
        candidate
            { promptSnapshotGeneratedContext =
                candidate.promptSnapshotGeneratedContext
                    <|> previous.promptSnapshotGeneratedContext
            , promptSnapshotGrokContext =
                candidate.promptSnapshotGrokContext
                    <|> previous.promptSnapshotGrokContext
            }
    | otherwise = candidate

promptSnapshotsSharePrefix
    :: SessionPromptSnapshot
    -> SessionPromptSnapshot
    -> Bool
promptSnapshotsSharePrefix left right =
    left.promptSnapshotVersion == right.promptSnapshotVersion
        && left.promptSnapshotProvider == right.promptSnapshotProvider
        && left.promptSnapshotConnection == right.promptSnapshotConnection
        && left.promptSnapshotModel == right.promptSnapshotModel
        && left.promptSnapshotDialect == right.promptSnapshotDialect
        && left.promptSnapshotCwd == right.promptSnapshotCwd
        && left.promptSnapshotInstructions == right.promptSnapshotInstructions
        && left.promptSnapshotTools == right.promptSnapshotTools
        && left.promptSnapshotCacheKey == right.promptSnapshotCacheKey

promptSnapshotsEquivalent
    :: SessionPromptSnapshot
    -> SessionPromptSnapshot
    -> Bool
promptSnapshotsEquivalent left right =
    promptSnapshotsSharePrefix left right
        && left.promptSnapshotGeneratedContext
            == right.promptSnapshotGeneratedContext
        && left.promptSnapshotGrokContext == right.promptSnapshotGrokContext

appendTurn :: SessionHandle -> SessionTurn -> IO SessionHandle
appendTurn handle turn =
    appendTurnWithMetaUpdate handle turn id

appendTurnIndexed :: SessionHandle -> SessionTurn -> IO (SessionHandle, Int64)
appendTurnIndexed handle turn =
    appendTurnWithMetaUpdateIndexed handle turn id

-- | Append one transcript turn, then apply an additional metadata transition
-- before the append's single metadata write.
appendTurnWithMetaUpdate
    :: SessionHandle
    -> SessionTurn
    -> (SessionMeta -> SessionMeta)
    -> IO SessionHandle
appendTurnWithMetaUpdate handle turn updateMeta =
    appendTurnWithMetaTransition handle turn
        (updateMeta . applyTurnMetadata turn)

appendTurnWithMetaUpdateIndexed
    :: SessionHandle
    -> SessionTurn
    -> (SessionMeta -> SessionMeta)
    -> IO (SessionHandle, Int64)
appendTurnWithMetaUpdateIndexed handle turn updateMeta =
    appendTurnWithMetaTransitionIndexed handle turn
        (updateMeta . applyTurnMetadata turn)

-- | Append a transcript turn and persist one metadata transition. Timestamp
-- and response-id updates are common to every kind of turn; the supplied
-- transition controls whether title, usage, or caller-specific metadata is
-- changed.
appendTurnWithMetaTransition
    :: SessionHandle
    -> SessionTurn
    -> (SessionMeta -> SessionMeta)
    -> IO SessionHandle
appendTurnWithMetaTransition handle turn transition = do
    fst <$> appendTurnWithMetaTransitionIndexed handle turn transition

appendTurnWithMetaTransitionIndexed
    :: SessionHandle
    -> SessionTurn
    -> (SessionMeta -> SessionMeta)
    -> IO (SessionHandle, Int64)
appendTurnWithMetaTransitionIndexed =
    appendTurnWithMetaTransitionIndexedUsing Store.appendSessionTurnIndexed

-- | Append a turn while atomically retiring the current provider-visible
-- prompt epoch. The returned handle has no prompt snapshot, so its next
-- request establishes a fresh stable prefix.
appendTurnWithPromptResetIndexed
    :: SessionHandle
    -> SessionTurn
    -> (SessionMeta -> SessionMeta)
    -> IO (SessionHandle, Int64)
appendTurnWithPromptResetIndexed handle turn transition =
    appendTurnWithMetaTransitionIndexedUsing
        Store.appendSessionTurnIndexedWithPromptReset
        handle
        turn
        (\meta ->
            (transition meta)
                { metaPromptSnapshot = Nothing
                })

appendTurnWithMetaTransitionIndexedUsing
    :: ( StorePool
        -> Store.SessionTurn
        -> Store.SessionMetadata
        -> IO (Either StoreError (Maybe Int64))
       )
    -> SessionHandle
    -> SessionTurn
    -> (SessionMeta -> SessionMeta)
    -> IO (SessionHandle, Int64)
appendTurnWithMetaTransitionIndexedUsing
    appendStoredTurn
    handle
    turn
    transition = do
    let pool = handle.sessionPool
    now <- normalizePostgresTimestamp <$> getCurrentTime
    let meta0 = handle.sessionMeta
        meta = meta0
            { metaUpdatedAt = now
            , metaLastResponseId = turn.turnResponseId <|> meta0.metaLastResponseId
            }
        finalMeta = transition meta
    appendStoredTurn
        pool
        (toStoredTurn turn)
        (toStoredMetadata finalMeta) >>= \case
            Left err ->
                fail
                    ("could not append PostgreSQL session turn: "
                        <> Text.unpack (renderStoreError err))
            Right Nothing ->
                fail ("session not found: " <> Text.unpack finalMeta.metaId)
            Right (Just turnIndex) ->
                pure (handle { sessionMeta = finalMeta }, turnIndex)

applyTurnMetadata :: SessionTurn -> SessionMeta -> SessionMeta
applyTurnMetadata turn meta =
    meta
        { metaTitle =
            if meta.metaTitle == "untitled" && not (Text.null turn.turnUserText)
                then sessionTitleFromPrompt turn.turnUserText
                else meta.metaTitle
        , metaInputTokens =
            meta.metaInputTokens + maybe 0 (.inputTokens) turn.turnUsage
        , metaOutputTokens =
            meta.metaOutputTokens + maybe 0 (.outputTokens) turn.turnUsage
        , metaCachedTokens =
            meta.metaCachedTokens + maybe 0 (.cachedTokens) turn.turnUsage
        }

-- | Persist provider usage that is not represented by its own transcript
-- turn, such as an inline compaction request. Session metadata is the
-- authoritative aggregate used when resuming.
addSessionUsage :: TokenUsage -> SessionHandle -> IO SessionHandle
addSessionUsage usage handle = do
    let pool = handle.sessionPool
    now <- normalizePostgresTimestamp <$> getCurrentTime
    let meta0 = handle.sessionMeta
        meta = meta0
            { metaUpdatedAt = now
            , metaInputTokens =
                meta0.metaInputTokens + usage.inputTokens
            , metaOutputTokens =
                meta0.metaOutputTokens + usage.outputTokens
            , metaCachedTokens =
                meta0.metaCachedTokens + usage.cachedTokens
            }
    Store.replaceSessionMetadata
        pool
        "session.usage_added"
        (toStoredMetadata meta) >>= \case
            Left err ->
                fail
                    ("could not update PostgreSQL session usage: "
                        <> Text.unpack (renderStoreError err))
            Right False ->
                fail ("session not found: " <> Text.unpack meta.metaId)
            Right True ->
                pure handle { sessionMeta = meta }

-- | The original root target under which metadata-less child transcripts may
-- have been created. Old session files derive it from their persisted root
-- target; once written, it remains stable across root model/provider changes.
sessionLegacySubagentTarget :: SessionMeta -> LegacySubagentTarget
sessionLegacySubagentTarget meta =
    fromMaybe
        LegacySubagentTarget
            { legacyTargetProvider = meta.metaProvider
            , legacyTargetConnection = meta.metaConnection
            , legacyTargetEffectiveModel =
                fromMaybe meta.metaModel meta.metaTransportModel
            , legacyTargetDialect = meta.metaDialect
            }
        meta.metaLegacySubagentTarget

sessionConversationText :: [SessionTurn] -> Text
sessionConversationText =
    Text.intercalate "\n\n" . foldMap renderTurn
  where
    renderTurn turn =
        [ "User:\n" <> turn.turnUserText ]
            <> case turn.turnAssistantText of
                Just text | not (Text.null (Text.strip text)) ->
                    ["Assistant:\n" <> text]
                _ -> []

sessionTitleTurnCountFromSlot
    :: Persistence
    -> IO Int
sessionTitleTurnCountFromSlot = \case
    PersistenceDisabled -> pure 0
    PersistenceEnabled slotRef ->
        readIORef slotRef >>= \case
            PersistencePending _ _ _ -> pure 0
            PersistenceActive handle -> pure handle.sessionMeta.metaTitleUserTurns

-- | Append a synthetic marker without deriving a title or aggregating usage.
-- Used for markers such as @/new@ and @/clear@.
appendTurnKeepTitle :: SessionHandle -> SessionTurn -> IO SessionHandle
appendTurnKeepTitle handle turn =
    appendTurnWithMetaTransition handle turn id

appendTurnKeepTitleIndexed
    :: SessionHandle
    -> SessionTurn
    -> IO (SessionHandle, Int64)
appendTurnKeepTitleIndexed handle turn =
    appendTurnWithMetaTransitionIndexed handle turn id

-- | Prompts in the current immutable transcript branch, paired with the turns
-- that should remain when rewinding to immediately before that prompt.
sessionRewindChoices :: [SessionTurn] -> [(SessionTurn, [SessionTurn])]
sessionRewindChoices turns =
    [ (turn, take turnIndex active)
    | (turnIndex, turn) <- zip [0 ..] active
    , isRewindPromptTurn turn
    ]
  where
    active =
        reverse
            (takeWhile
                ((/= TranscriptReset) . (.turnEffect))
                (reverse turns))

-- | Publish a rewound conversation branch without mutating historical turns.
--
-- The reset marker and retained prefix are appended atomically. Replayed
-- compaction checkpoints keep their replace effect, so model context resumes
-- from the correct compacted suffix while the full visual prefix stays
-- scrollable.
rewindSession
    :: SessionHandle
    -> [SessionTurn]
    -> IO (Either Text SessionHandle)
rewindSession handle retained = do
    now <- normalizePostgresTimestamp <$> getCurrentTime
    let promptCount = length (filter isRewindPromptTurn retained)
        meta0 = handle.sessionMeta
        meta = meta0
            { metaUpdatedAt = now
            , metaLastResponseId = retainedLastResponseId retained
            , metaTitleRefreshIndex =
                min
                    meta0.metaTitleRefreshIndex
                    (titleRefreshIndex promptCount)
            , metaTitleUserTurns = promptCount
            , metaLastRecap = Nothing
            , metaLastTurnSummary = Nothing
            , metaLastRecapMainTurns = 0
            }
        marker = SessionTurn
            { turnAt = now
            , turnUserText = rewindSessionUserText
            , turnAssistantText = Just "Conversation rewound."
            , turnError = Nothing
            , turnResponseId = Nothing
            , turnEffect = TranscriptReset
            , turnItems = []
            , turnUsage = Nothing
            , turnProviderTelemetry = []
            }
    Store.appendSessionTurns
        handle.sessionPool
        (map toStoredTurn (marker : retained))
        (toStoredMetadata meta) >>= \case
            Left err ->
                pure
                    (Left
                        ("could not rewind PostgreSQL session: "
                            <> renderStoreError err))
            Right False ->
                pure (Left ("session not found: " <> meta.metaId))
            Right True ->
                pure (Right handle { sessionMeta = meta })

isRewindPromptTurn :: SessionTurn -> Bool
isRewindPromptTurn turn =
    turn.turnEffect == TranscriptAppend
        && not (Text.null (Text.strip turn.turnUserText))

retainedLastResponseId :: [SessionTurn] -> Maybe Text
retainedLastResponseId = foldl' step Nothing
  where
    step responseId turn = case turn.turnEffect of
        TranscriptAppend -> turn.turnResponseId <|> responseId
        TranscriptReplace -> turn.turnResponseId
        TranscriptReset -> turn.turnResponseId


loadSession
    :: StorePool
    -> OsPath
    -> Text
    -> IO (Either Text (SessionMeta, [SessionTurn]))
loadSession pool root sessionId = runExceptT do
    _ <- except (sessionDirForId root sessionId)
    stored <- loadWithLegacyImport root pool sessionId Store.loadSession
    storedPrompt <- loadStoredPromptEpoch pool sessionId
    decodeStoredSession
        sessionSchemaVersion
        isValidSessionId
        sessionId
        storedPrompt
        stored

loadActiveSession
    :: StorePool
    -> OsPath
    -> Text
    -> IO (Either Text (SessionMeta, [SessionTurn]))
loadActiveSession pool root sessionId = runExceptT do
    _ <- except (sessionDirForId root sessionId)
    stored <- loadWithLegacyImport root pool sessionId Store.loadActiveSession
    storedPrompt <- loadStoredPromptEpoch pool sessionId
    decodeStoredSession
        sessionSchemaVersion
        isValidSessionId
        sessionId
        storedPrompt
        stored

loadStoredPromptEpoch
    :: StorePool
    -> Text
    -> ExceptT Text IO (Maybe Store.SessionPromptEpoch)
loadStoredPromptEpoch pool sessionId =
    lift (Store.loadLatestSessionPromptEpoch pool sessionId)
        >>= either (throwE . renderStoreError) pure

loadSessionMeta
    :: StorePool
    -> OsPath
    -> Text
    -> IO (Either Text SessionMeta)
loadSessionMeta pool root sessionId = runExceptT do
    _ <- except (sessionDirForId root sessionId)
    stored <- loadWithLegacyImport root pool sessionId Store.loadSessionMetadata
    meta <- except (fromStoredMetadata stored)
    validateSessionMeta sessionSchemaVersion isValidSessionId sessionId meta
    pure meta

loadRecentSessionTurns
    :: StorePool
    -> OsPath
    -> Text
    -> Int
    -> IO (Either Text SessionTurnPage)
loadRecentSessionTurns pool root sessionId limit =
    loadSessionTurnPage root pool sessionId
        (\pool' key -> Store.loadRecentSessionTurns pool' key limit)

loadSessionTurnsBefore
    :: StorePool
    -> OsPath
    -> Text
    -> Int64
    -> Int
    -> IO (Either Text SessionTurnPage)
loadSessionTurnsBefore pool root sessionId cursor limit =
    loadSessionTurnPage root pool sessionId
        (\pool' key -> Store.loadSessionTurnsBefore pool' key cursor limit)

loadSessionTurnsAfter
    :: StorePool
    -> OsPath
    -> Text
    -> Int64
    -> Int
    -> IO (Either Text SessionTurnPage)
loadSessionTurnsAfter pool root sessionId cursor limit =
    loadSessionTurnPage root pool sessionId
        (\pool' key -> Store.loadSessionTurnsAfter pool' key cursor limit)

loadSessionResumeStats
    :: StorePool
    -> OsPath
    -> Text
    -> IO (Either Text SessionResumeStats)
loadSessionResumeStats pool root sessionId = runExceptT do
    _ <- except (sessionDirForId root sessionId)
    stored <- loadWithLegacyImport root pool sessionId Store.loadSessionResumeStats
    pure SessionResumeStats
        { resumeStatsTurnCount = fromIntegral stored.sessionResumeTurnCount
        , resumeStatsMessageCount = fromIntegral stored.sessionResumeMessageCount
        , resumeStatsToolCount = fromIntegral stored.sessionResumeToolCount
        , resumeStatsFirstPrompt =
            fmap Text.strip stored.sessionResumeFirstPrompt
        }

loadSessionTurnPage
    :: OsPath
    -> StorePool
    -> Text
    -> (StorePool -> Text
        -> IO (Either StoreError (Maybe Store.SessionTurnPage)))
    -> IO (Either Text SessionTurnPage)
loadSessionTurnPage root pool sessionId loader = runExceptT do
    _ <- except (sessionDirForId root sessionId)
    stored <- loadWithLegacyImport root pool sessionId loader
    turns <- except $ traverse
        (\storedTurn -> do
            turn <- fromStoredTurn storedTurn.storedTurn
            pure (storedTurn.storedTurnIndex, turn))
        (Vector.toList stored.sessionPageTurns)
    pure SessionTurnPage
        { pageTurns = turns
        , pageGenerationStart = stored.sessionPageGenerationStart
        , pageTotalTurns = stored.sessionPageTotal
        , pageHasOlder = stored.sessionPageHasOlder
        , pageHasNewer = stored.sessionPageHasNewer
        }

loadWithLegacyImport
    :: OsPath
    -> StorePool
    -> Text
    -> (StorePool -> Text -> IO (Either StoreError (Maybe a)))
    -> ExceptT Text IO a
loadWithLegacyImport root pool sessionId loader = do
    stored <- lift (loader pool sessionId)
        >>= either (throwE . renderStoreError) pure
    stored' <- case stored of
        Just value -> pure (Just value)
        Nothing -> do
            _ <- importLegacySession
                sessionSchemaVersion
                isValidSessionId
                (sessionDirForId root)
                pool
                sessionId
            -- Another process may win the import race and return False from
            -- its idempotent insert. Always reload the canonical row.
            lift (loader pool sessionId)
                >>= either (throwE . renderStoreError) pure
    maybe (throwE ("session not found: " <> sessionId)) pure stored'

-- | Load several sessions with one batched PostgreSQL read while preserving
-- request order. A missing database row still takes the legacy import path.
loadSessions
    :: StorePool
    -> OsPath
    -> [Text]
    -> IO [Either Text (SessionMeta, [SessionTurn])]
loadSessions pool root sessionIds = do
    let validated =
            [ sessionDirForId root sessionId
                >> Right sessionId
            | sessionId <- sessionIds
            ]
        validIds = [sessionId | Right sessionId <- validated]
    stored <- Store.loadSessions pool validIds
    restoreResults validated stored
  where
    restoreResults [] [] = pure []
    restoreResults (Left err : rest) results =
        (Left err :) <$> restoreResults rest results
    restoreResults (Right sessionId : rest) (result : results) = do
        loaded <- case result of
            Left err -> pure (Left (renderStoreError err))
            Right Nothing -> loadSession pool root sessionId
            Right (Just value) ->
                runExceptT
                    (decodeStoredSession
                        sessionSchemaVersion
                        isValidSessionId
                        sessionId
                        Nothing
                        value)
        (loaded :) <$> restoreResults rest results
    restoreResults _ _ =
        pure [Left "batched session load returned an unexpected result count"]

loadSessionHandle
    :: StorePool
    -> OsPath
    -> Text
    -> IO (Either Text (SessionHandle, [SessionTurn]))
loadSessionHandle pool root sessionId =
    loadActiveSession pool root sessionId >>= \case
        Left err -> pure (Left err)
        Right (meta, turns) ->
            pure do
                dir <- sessionDirForId root sessionId
                tempDir <- sessionTempDirForId root sessionId
                Right
                    ( SessionHandle
                        { sessionPool = pool
                        , sessionDir = dir
                        , sessionTempDir = tempDir
                        , sessionMetaPath = dir </> unsafeEncodeUtf "meta.json"
                        , sessionTranscriptPath =
                            dir </> unsafeEncodeUtf "transcript.jsonl"
                        , sessionMeta = meta
                        }
                    , turns
                    )

-- | Import a transferred session under its existing id and optional cwd.
importSessionTransfer
    :: StorePool
    -> OsPath
    -> Maybe OsPath
    -> SessionTransfer
    -> IO (Either Text Text)
importSessionTransfer pool root cwd transfer = runExceptT do
    let sessionId = transfer.transferMeta.metaId
    dir <- except (sessionDirForId root sessionId)
    exists <- lift (doesDirectoryExist dir)
    when exists (throwE ("session already exists: " <> sessionId))
    lift (ensurePrivateDir root)
    lift (createDirectory dir)
    lift (setFileMode (unsafeToFilePath dir) 0o700)
    _ <- lift (ensureSessionTemp root sessionId) >>= except
    let meta = transfer.transferMeta
            { metaCwd = fromMaybe transfer.transferMeta.metaCwd cwd }
        bytes = Aeson.encode (SessionTransfer meta transfer.transferTurns)
        legacy = Store.LegacySession
            { legacySourcePath = "afk:" <> transfer.transferMeta.metaId
            , legacyContentHash = contentFingerprint bytes
            , legacyMetadata = toStoredMetadata meta
            , legacyTurns = map toStoredTurn transfer.transferTurns
            , legacyPromptSnapshot =
                toStoredPromptSnapshot <$> meta.metaPromptSnapshot
            }
    lift (Store.importLegacySession pool legacy) >>= \case
        Left err -> do
            lift (cleanupTransfer dir sessionId)
            throwE (renderStoreError err)
        Right False -> do
            lift (cleanupTransfer dir sessionId)
            throwE ("session already exists: " <> sessionId)
        Right True -> pure sessionId
  where
    cleanupTransfer dir sessionId = do
        _ <- tryIO (removePathForcibly dir)
        _ <- removeSessionTemp root sessionId
        pure ()

deleteSession :: StorePool -> OsPath -> Text -> IO (Either Text ())
deleteSession pool root sessionId = runExceptT do
    dir <- except (sessionDirForId root sessionId)
    exists <- lift (doesDirectoryExist dir)
    lock <- if exists
        then lift (acquireSessionLock dir sessionId) >>= \case
            Left _ -> throwE "cannot delete a running session"
            Right lock -> pure (Just lock)
        else pure Nothing
    now <- lift getCurrentTime
    deleted <- lift $
        Store.deleteSession pool sessionId now
            `finally` maybe (pure ()) releaseSessionLock lock
    case deleted of
        Left err -> throwE (renderStoreError err)
        Right False -> throwE ("session not found: " <> sessionId)
        Right True
            | not exists -> pure ()
            | otherwise ->
                lift (tryIO (removePathForcibly dir)) >>= \case
                    Left err ->
                        throwE
                            ("session deleted but artifacts could not be removed: "
                                <> Text.pack (displayException err))
                    Right () -> pure ()
    tempRemoved <- lift (removeSessionTemp root sessionId)
    except tempRemoved

-- | Session ids are single path components. Keep this deliberately broader
-- than the current date-plus-hex allocator so older ids remain resumable.
isValidSessionId :: Text -> Bool
isValidSessionId sessionId =
    not (Text.null sessionId)
        && sessionId /= "."
        && sessionId /= ".."
        && Text.all (\char -> char /= '/' && char /= '\\' && char /= '\NUL') sessionId

sessionDirForId :: OsPath -> Text -> Either Text OsPath
sessionDirForId root sessionId
    | isValidSessionId sessionId =
        Right (root </> unsafeEncodeUtf (Text.unpack sessionId))
    | otherwise = Left "invalid session id"

sessionTempDirForId :: OsPath -> Text -> Either Text OsPath
sessionTempDirForId root sessionId
    | isValidSessionId sessionId =
        Right
            (sessionTempsRoot root
                </> unsafeEncodeUtf (Text.unpack sessionId))
    | otherwise = Left "invalid session id"

listSessions :: StorePool -> OsPath -> IO ([SessionMeta], [Text])
listSessions pool _root = do
    Store.listSessionMetadata pool >>= \case
        Left err ->
            fail
                ("could not list PostgreSQL sessions: "
                    <> Text.unpack (renderStoreError err))
        Right values ->
            let decoded = map decodeListedSessionMeta values
            in pure
                ( [meta | Right meta <- decoded]
                , [err | Left err <- decoded]
                )

-- | Decode one persisted session for listing. Corrupt or incompatible
-- metadata becomes an error string instead of disappearing from the picker.
decodeListedSessionMeta :: Store.SessionMetadata -> Either Text SessionMeta
decodeListedSessionMeta value = do
    meta <- fromStoredMetadata value
    unless (meta.metaVersion == sessionSchemaVersion) $
        Left $
            "unsupported session schema version "
                <> Text.pack (show meta.metaVersion)
                <> " for session "
                <> meta.metaId
                <> " (expected "
                <> Text.pack (show sessionSchemaVersion)
                <> ")"
    pure meta

writeSessionMeta :: StorePool -> OsPath -> SessionMeta -> IO ()
writeSessionMeta pool _path meta = do
    Store.replaceSessionMetadata
        pool
        "session.metadata_replaced"
        (toStoredMetadata meta) >>= \case
            Left err ->
                fail
                    ("could not update PostgreSQL session metadata: "
                        <> Text.unpack (renderStoreError err))
            Right False ->
                fail ("session not found: " <> Text.unpack meta.metaId)
            Right True -> pure ()

sessionTitleFromPrompt :: Text -> Text
sessionTitleFromPrompt prompt =
    let title = case take 10 (Text.words (Text.strip prompt)) of
            [] -> "New session"
            words' -> Text.unwords words'
    in if Text.length title <= 72
        then title
        else Text.take 69 title <> "..."

setGeneratedSessionTitle :: Int -> Text -> SessionHandle -> IO SessionHandle
setGeneratedSessionTitle refreshIndex rawTitle handle
    | handle.sessionMeta.metaTitleIsManual = pure handle
    | otherwise = writeTitle False refreshIndex rawTitle handle

setManualSessionTitle :: Text -> SessionHandle -> IO SessionHandle
setManualSessionTitle = writeTitle True 2

writeTitle :: Bool -> Int -> Text -> SessionHandle -> IO SessionHandle
writeTitle manual refreshIndex rawTitle handle = do
    let title = Text.unwords (Text.words (Text.strip rawTitle))
        meta = handle.sessionMeta
            { metaTitle = title
            , metaTitleIsManual = manual
            , metaTitleRefreshIndex = refreshIndex
            }
    writeSessionMeta handle.sessionPool handle.sessionMetaPath meta
    pure handle { sessionMeta = meta }

resetSessionTitleToAuto :: SessionHandle -> IO SessionHandle
resetSessionTitleToAuto handle = do
    let meta = handle.sessionMeta
            { metaTitleIsManual = False
            , metaTitleRefreshIndex = 0
            }
    writeSessionMeta handle.sessionPool handle.sessionMetaPath meta
    pure handle { sessionMeta = meta }

setSessionRecap :: Text -> Int -> SessionHandle -> IO SessionHandle
setSessionRecap summary mainTurns handle = do
    let meta = handle.sessionMeta
            { metaLastRecap = Just summary
            , metaLastRecapMainTurns = max 0 mainTurns
            }
    writeSessionMeta handle.sessionPool handle.sessionMetaPath meta
    pure handle { sessionMeta = meta }

setSessionTurnSummary :: Text -> SessionHandle -> IO SessionHandle
setSessionTurnSummary summary handle = do
    let meta = handle.sessionMeta { metaLastTurnSummary = Just summary }
    writeSessionMeta handle.sessionPool handle.sessionMetaPath meta
    pure handle { sessionMeta = meta }

sessionUsageFromMeta :: SessionMeta -> TokenUsage
sessionUsageFromMeta meta = TokenUsage
    { inputTokens = meta.metaInputTokens
    , outputTokens = meta.metaOutputTokens
    , cachedTokens = meta.metaCachedTokens
    }

-- | Session totals come from meta. Older sessions without those fields
-- decode as zero and start accumulating from new turns.
sessionUsageFromTurns :: SessionMeta -> [SessionTurn] -> TokenUsage
sessionUsageFromTurns meta _turns = sessionUsageFromMeta meta

-- | Copy-pasteable resume line printed on Ctrl-C, matching grok build.
-- The bare @--resume@ form resolves the latest session for the directory it
-- is started from, which is the session that was just persisted.
resumeHint :: String -> Text
resumeHint progName =
    "Resume this session with: " <> shellSingleQuote progName <> " --resume"

-- | POSIX single-quote so paths with spaces stay one shell word.
shellSingleQuote :: String -> Text
shellSingleQuote s =
    "'" <> Text.replace "'" "'\\''" (Text.pack s) <> "'"

-- | Reserve a unique session id by atomically creating its private scratch
-- directory. The durable session directory remains deferred until first use.
allocateSessionTemp :: OsPath -> IO (Text, OsPath)
allocateSessionTemp root = do
    let tempRoot = sessionTempsRoot root
    ensurePrivateDir tempRoot
    now <- getCurrentTime
    go tempRoot now (0 :: Int)
  where
    go tempRoot now attempt
        | attempt >= 32 = fail "could not allocate a unique session temp directory"
        | otherwise = do
            let sessionId = sessionIdForAttempt now attempt
                durableDir =
                    root </> unsafeEncodeUtf (Text.unpack sessionId)
                tempDir =
                    tempRoot </> unsafeEncodeUtf (Text.unpack sessionId)
            durableExists <-
                maybe False (const True)
                    <$> symbolicLinkStatusMaybe durableDir
            if durableExists
                then go tempRoot now (attempt + 1)
                else tryIO (createDirectory tempDir) >>= \case
                    Left _ -> go tempRoot now (attempt + 1)
                    Right () -> do
                        setFileMode (unsafeToFilePath tempDir) 0o700
                        pure (sessionId, tempDir)

-- | Take a shared lease for a session's scratch directory. Automatic cleanup
-- requires the matching exclusive lock, so a live process cannot lose its
-- temporary files even before its durable session lock has been acquired.
acquireSessionTempLease
    :: OsPath
    -> OsPath
    -> IO (Either Text (Maybe SessionTempLease))
acquireSessionTempLease root path =
    case sessionTempId root path of
        Nothing -> pure (Right Nothing)
        Just sessionId -> do
            let lockPath = sessionTempLockPath root sessionId
            result <- tryAny $
                ensurePrivateDir (takeDirectory lockPath)
                    >> FileLock.tryLockFile
                        (unsafeToFilePath lockPath)
                        FileLock.Shared
            pure case result of
                Left exception ->
                    Left
                        ("failed to lease session scratch directory "
                            <> toText path
                            <> ": "
                            <> Text.pack (displayException exception))
                Right Nothing ->
                    Left
                        ("session scratch directory is being cleaned up: "
                            <> toText path)
                Right (Just lock) ->
                    Right (Just (SessionTempLease lock))

releaseSessionTempLease :: SessionTempLease -> IO ()
releaseSessionTempLease (SessionTempLease lock) = do
    _ <- tryAny (FileLock.unlockFile lock)
    pure ()

-- | Remove old session scratch directories after retaining the newest entries.
-- Only allocator-shaped names are considered, and a directory with a live
-- shared lease is skipped. Failures are reported per path and never stop the
-- rest of the best-effort cleanup. Directories allocated on the current UTC
-- day are always kept, closing the startup interval before a lease is taken.
cleanupStaleSessionTemps
    :: OsPath
    -> Int
    -> [OsPath]
    -> IO SessionTempCleanupReport
cleanupStaleSessionTemps root requestedKeep protected = do
    let tempRoot = sessionTempsRoot root
    exists <- doesDirectoryExist tempRoot
    if not exists
        then pure mempty
        else do
            today <- utctDay <$> getCurrentTime
            listed <- tryAny (listDirectory tempRoot)
            case listed of
                Left exception ->
                    pure $ tempCleanupFailure tempRoot exception
                Right entries -> do
                    directories <- foldM
                        (collectDirectory tempRoot)
                        ([], [])
                        entries
                    case directories of
                        (managed, discoveryFailures) -> do
                            let candidates =
                                    filter
                                        (isBefore today . takeFileName)
                                        (drop (max 1 requestedKeep) $
                                            sortOn
                                                (Down
                                                    . unsafeToFilePath
                                                    . takeFileName)
                                                managed)
                            cleaned <- foldM cleanupOne mempty candidates
                            pure $
                                cleaned
                                    <> mempty
                                        { tempCleanupFailures =
                                            discoveryFailures
                                        }
  where
    protectedPaths = map normalise protected
    isBefore today path =
        maybe False (< today) (allocatedSessionDay path)

    collectDirectory tempRoot (managed, failures) entry = do
        let path = tempRoot </> entry
        checked <- tryAny (doesDirectoryExist path)
        pure case checked of
            Left exception ->
                ( managed
                , failures
                    <> [(path, Text.pack (displayException exception))]
                )
            Right True
                | isAllocatedSessionId entry ->
                    (managed <> [path], failures)
            Right _ -> (managed, failures)

    cleanupOne report candidate
        | any
            (\protectedPath ->
                equalFilePath protectedPath (normalise candidate))
            protectedPaths =
                pure report
        | otherwise = do
            result <- tryAny (cleanupStaleSessionTemp root candidate)
            pure $ report <> case result of
                Left exception ->
                    tempCleanupFailure candidate exception
                Right candidateReport -> candidateReport

cleanupStaleSessionTemp
    :: OsPath
    -> OsPath
    -> IO SessionTempCleanupReport
cleanupStaleSessionTemp root candidate =
    case sessionTempId root candidate of
        Nothing -> pure mempty
        Just sessionId -> do
            let durableDir = root </> sessionId
            durableExists <- doesDirectoryExist durableDir
            durableLock <-
                if durableExists
                    then fmap (fmap Just) $
                        acquireSessionLock durableDir (toText sessionId)
                    else pure (Right Nothing)
            case durableLock of
                -- A running or otherwise un-lockable durable session owns the
                -- scratch directory. Treat either case conservatively.
                Left _ -> pure mempty
                Right lock ->
                    cleanupWithSessionLock sessionId
                        `finally` mapM_ releaseSessionLock lock
  where
    cleanupWithSessionLock sessionId = do
        let lockPath = sessionTempLockPath root sessionId
        locked <- tryAny $
            ensurePrivateDir (takeDirectory lockPath)
                >> FileLock.tryLockFile
                    (unsafeToFilePath lockPath)
                    FileLock.Exclusive
        case locked of
            Left exception ->
                pure $ tempCleanupFailure candidate exception
            Right Nothing ->
                pure mempty
            Right (Just lock) -> do
                removed <- tryAny $
                    (do
                        symbolicLinkStatusMaybe candidate >>= \case
                            -- Another startup cleaner may have removed the
                            -- candidate before this process acquired its
                            -- exclusive lock.
                            Nothing -> pure False
                            Just status
                                | isSymbolicLink status -> pure False
                                | otherwise ->
                                    removePathForcibly candidate >> pure True)
                        `finally` FileLock.unlockFile lock
                pure case removed of
                    Left exception ->
                        tempCleanupFailure candidate exception
                    Right True ->
                        mempty { tempCleanupRemoved = [candidate] }
                    Right False -> mempty

sessionTempId :: OsPath -> OsPath -> Maybe OsPath
sessionTempId root rawPath =
    let tempRoot = normalise (sessionTempsRoot root)
        path = normalise rawPath
    in if equalFilePath tempRoot (takeDirectory path)
            && isAllocatedSessionId (takeFileName path)
        then Just (takeFileName path)
        else Nothing

sessionTempLockPath :: OsPath -> OsPath -> OsPath
sessionTempLockPath root sessionId =
    sessionTempsRoot root
        </> unsafeEncodeUtf ".locks"
        </> (sessionId <> unsafeEncodeUtf ".lock")

isAllocatedSessionId :: OsPath -> Bool
isAllocatedSessionId path = case allocatedSessionDay path of
    Just _ -> True
    Nothing -> False

allocatedSessionDay :: OsPath -> Maybe Day
allocatedSessionDay path =
    case unsafeToFilePath path of
        year1 : year2 : year3 : year4 : '-' :
                month1 : month2 : '-' : day1 : day2 : '-' : suffix ->
            let date =
                    [ year1, year2, year3, year4, '-'
                    , month1, month2, '-', day1, day2
                    ]
            in if length suffix == 8 && all isHexDigit suffix
                then parseTimeM True defaultTimeLocale "%Y-%m-%d" date
                else Nothing
        _ -> Nothing

tempCleanupFailure
    :: OsPath
    -> SomeException
    -> SessionTempCleanupReport
tempCleanupFailure path exception =
    mempty
        { tempCleanupFailures =
            [(path, Text.pack (displayException exception))]
        }

ensureSessionTemp :: OsPath -> Text -> IO (Either Text OsPath)
ensureSessionTemp root sessionId =
    case sessionTempDirForId root sessionId of
        Left err -> pure (Left err)
        Right tempDir -> do
            result <- tryIO (ensurePrivateDir tempDir)
            pure $ case result of
                Left err ->
                    Left
                        ("could not create session temp directory: "
                            <> Text.pack (displayException err))
                Right () -> Right tempDir

sessionIdForAttempt :: UTCTime -> Int -> Text
sessionIdForAttempt now attempt =
    let day = formatTime defaultTimeLocale "%Y-%m-%d" now
        start =
            floor
                (nominalDiffTimeToSeconds
                    (utcTimeToPOSIXSeconds now)
                    * 1000000) :: Integer
        hex = hex8 (start + fromIntegral attempt)
    in Text.pack (day <> "-" <> hex)

removeSessionTemp :: OsPath -> Text -> IO (Either Text ())
removeSessionTemp root sessionId =
    case sessionTempDirForId root sessionId of
        Left err -> pure (Left err)
        Right tempDir -> do
            exists <- doesDirectoryExist tempDir
            if not exists
                then pure (Right ())
                else tryIO (removePathForcibly tempDir) >>= \case
                    Left err ->
                        pure $ Left
                            ("could not delete session temp directory: "
                                <> Text.pack (displayException err))
                    Right () -> pure (Right ())

removeReservedTemp :: OsPath -> Text -> IO ()
removeReservedTemp root sessionId = do
    _ <- removeSessionTemp root sessionId
    pure ()

hex8 :: Integer -> String
hex8 n =
    let s = showHex (n `mod` 0x100000000) ""
    in replicate (8 - length s) '0' <> s

ensurePrivateDir :: OsPath -> IO ()
ensurePrivateDir path = do
    createDirectoryIfMissing True path
    _ <- tryIO (setFileMode (unsafeToFilePath path) 0o700)
    pure ()
